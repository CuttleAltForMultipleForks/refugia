title: Supreme Court Ruling
subtitle: The Supreme Court has made its ruling...
view-if: time >= 4
tags: event
face-image: img/supreme_court.png
max-visits: 1
new-page: true
go-to: ppp_pov if party == "PPP"; pnu_pov if party == "PNU"; pcr_pov if party == "PCR"

@ppp_pov

After a couple weeks of deliberation, the Supreme Court has made its ruling. We'll have to see if the pressure we have applied on them through our protests will be enough to ensure they uphold justice.

- @ruling: The Supreme Court rules...

@ruling
go-to: lg_banned if strife+ppp_judiciary < pnu_judiciary; lorenzo_arrested if strife+ppp_judiciary >= pnu_judiciary

@lg_banned
on-arrival: ppp_police -= 1; police_loyalty -= 0.05; scardinaist_strength -= 4; caterinoist_strength += 4; guagentist_strength += 2; pnu_turnout += 0.04; workers_pnu += 3; workers_turnout += 0.01; new_middle_turnout += 0.01; old_middle_turnout += 0.01; polarization = 1;

In an outrageous [? if ppp_judiciary < 2 : 4-1 ?][? if ppp_judiciary >= 2 : 3-2 ?] ruling, the Supreme Court has found that the Liberty Guard is an illegal organization, obligating us in a court order to ban them. [? if strife >= 2 : With protests mounting, we may be able to challenge them on this. ?]

- @people_court: Establish the People's Courts via presidential decree!
- @defy: Defy the ruling, and refuse to ban the Liberty Guard.
- @oblige: Oblige and ban the Liberty Guard.

@lorenzo_arrested
on-arrival: ppp_police -= 1; police_loyalty -= 0.04; guagentist_strength += 3; pnu_turnout += 0.02; 

In an outrageous [? if ppp_judiciary < 2 : and unanimous?][? if ppp_judiciary >= 2 : 4-1 ?] ruling, the Supreme Court has found that Sandro Lorenzo is guilty of insurrection, treason and conspiracy, despite him doing no wrong and being nothing but a victim of police corruption and abuse.

- @pardon_lorenzo: Pardon Lorenzo, for he has done no wrong.
- @campaign_against: Ramp up the protests, but take no concrete action.
- @silence: Let him fall. We have more important matters to deal with.

@pardon_lorenzo
on-arrival: scardinaist_dissent += 2; old_middle_pnu += 3; new_middle_pnu += 1; freeholder_pnu += 3; workers_ppp -= 2; pnu_turnout += 0.01; new_middle_ppp += strife

We pardon Lorenzo, motivating the <span style="color:rgb(64, 89, 156);">**PNU**</span> to attack us for supposed intervention in the judicial process and yet another case of overreach. Meanwhile, Lorenzo himself finds himself hailed as though a celebrity by the jubilant crowds of our supporters, and has been propelled into national importance.

@campaign_against
subtitle: -1 resource.
choose-if: resources >= 1
on-arrival: resources -= 1; lg_strength += 10; ppp_turnout += 0.01; strife += 1; workers_ppp += 2*(1-dissent); peasant_ppp += 4*(1-dissent); new_middle_ppp += 3*(1-dissent); workers_turnout += 0.01; new_middle_turnout += 0.02; peasant_turnout += 0.02; polarization = 1

The protests build with the news of Lorenzo's conviction, our attempts to martyr him a success. Lorenzo himself evades arrest, fleeing to our party offices in Tulanto. The <span style="color:rgb(64, 89, 156);">**PNU**</span> attacks us for sheltering a criminal and labels us crooks, but the people's hearts have been won over, and thankfully for us, not by them.

@silence
on-arrival: caterinoist_dissent += 10; scardinaist_strength += 5; pnu_judiciary -= 1 if ppp_judiciary > 1; business_strength += 4; guagentist_strength += 4; revolutionary_fatigue -= 1; strife -= 1; pcr_relation -= 5; pnu_relation += 2; ppp_turnout -= 0.05; pnu_turnout += 0.02;

We do not take action, bewildering the Caterinoists in our party, who take Lorenzo in anyway, causing the <span style="color:rgb(64, 89, 156);">**PNU**</span> to attack us as crooks and criminals. Despite the setbacks, we have managed to build bridges with more moderate groups in the establishment by our inaction, however. Let's hope it's worth it.

@people_court
choose-if: emergency_rule
unavailable-subtitle: We lack the sufficient power.
on-arrival: peoples_court = 1; budget -= 1 if resources < 2; resources -= 2 if resources >= 2; scardinaist_dissent += 3; ppp_police += 1; pnu_police -= 1; pnu_gov -= 1; ppp_judiciary += 2; pnu_judiciary -= 1; revolutionary_fatigue += 1; strife += 1; old_middle_ppp -= 3; freeholder_ppp -= 3; peasant_ppp += 4*(1-dissent); workers_ppp += 2*(1-dissent); pnu_turnout += 0.02
subtitle: [? if resources >= 2 : -2 resources. ?][? if resources < 2 : -1 budget. ?] This may upset the Scardinaists.

> *"In accordance with our belief that the People's Will is the highest authority in the nation, a new judicial system of People's Courts shall be established that hold authority over all judicial instruments and institutions established prior to the revolution."*

 - 1920 People's Courts Decree

The establishment of the People's Courts is an overwhelming success, as we devote our resources to making sure every town and city in the country may be free from the yoke of the olden Monarchy's Courts. The <span style="color:rgb(64, 89, 156);">**PNU**</span> boycotts its own right to participate in selecting the justices, further cementing our dominance and allowing us to pursue through these courts a progressive agenda.

The <span style="color:rgb(64, 89, 156);">**PNU**</span> is shocked by the speed at which the people have made their voices one, and is yet to respond, but we can be sure that the Supreme Court will strike it down as unconstitutional. We should prepare for a constitutional crisis, or worse. 

@defy
on-arrival: pnu_police += 1; ppp_gov -= 1; labor_dissent += 2; caterinoist_strength -= 3; scardinaist_dissent += 5; revolutionary_fatigue += 2; pro_republic -= 5; capital_strike_progress += 1; pnu_turnout += 0.03

We refuse to take action against the Liberty Guard, which has done no wrong, triggering a constitutional crisis as the <span style="color:rgb(64, 89, 156);">**PNU**</span> attacks us for ignoring a court order and rallies for "democracy".

@oblige
choose-if: scardinaist_strength >= 200
unavailable-subtitle: Are you stupid?? No.

@pnu_pov

After a couple weeks of deliberation, the Supreme Court has made its ruling. We'll have to see if they make the right decision despite the <span style="color:rgb(238, 117, 36);">**PPP**</span>'s attempts to intimidate them.

- @ruling_pnu: The Supreme Court rules...

@ruling_pnu
go-to: lg_banned_pnu if tension_path; lorenzo_arrest_pnu if lorenzo_path

@lg_banned_pnu
on-arrival: peoples_court = 1; ppp_police -= 1; police_loyalty -= 0.05; scardinaist_strength -= 4; caterinoist_strength += 4; guagentist_strength += 2; pnu_turnout += 0.04; workers_pnu += 3; workers_turnout += 0.01; new_middle_turnout += 0.01; old_middle_turnout += 0.01; polarization = 1;

In a 4-1 ruling, the Supreme Court has found that the Liberty Guard is an illegal organization, obligating the crooked Caterino to ban them. He is yet to either follow the court order or defy it, but we can be fairly confident in which route he will take.

- @coming_storm: We must prepare for the coming storm.
- @cooler_heads: Cooler heads must prevail.

@coming_storm
on-arrival: fascist_strength += 5; church_strength += 3; guagentist_dissent += 2; parallel_cost = 2;

We must prepare for the coming storm. To that end we will make preparations for the establishment of multiple party-affiliated organizations ready to organize in defense of our nation.

@cooler_heads
on-arrival: guagentist_strength += 4; business_strength += 2; fascist_dissent += 2; parallel_cost = 3;

Cooler heads must prevail. With the courts and police on our side, the only way we can lose is recklesness. Caterino will fall sooner or later, and we must not antagonize the liberals in his party who would happily see him gone.

@lorenzo_arrest_pnu
on-arrival: ppp_police -= 1; police_loyalty -= 0.04; guagentist_strength += 3; pnu_turnout += 0.02; 

In a unanimous ruling, Sandro Lorenzo has been found guilty of treason, insurrection and conspiracy. Caterino then has the gall to defy the court and intervene in the judicial process by directly pardoning him. 


@pcr_pov

Boo!!